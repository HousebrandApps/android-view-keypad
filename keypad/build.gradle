apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

version = "1.0.1"

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 4
        versionName "1.0.1"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.10.0'
}


def siteUrl = 'https://github.com/HousebrandApps/android-view-keypad'
def gitUrl = 'https://github.com/HousebrandApps/android-view-keypad.git'
group = "com.housebrandapps"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'

                name 'com.housebrandapps:android-view-keypad'
                description = 'A simple KeyPad widget library for android. Does not require the Soft Keyboard, is Databinding ready and highly customisable.'
                url siteUrl

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'njdwklopper'
                        name 'Jonathan Klopper'
                        email 'natieklopper@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}


bintray {
    def binUser = hasProperty('bintrayuser') ? binUser : System.getenv('bintrayuser')
    def binApi = hasProperty('bintrayapikey') ? binApi : System.getenv('bintrayapikey')
    def binPwd = hasProperty('bintraygpgpassword') ? binPwd : System.getenv('bintraygpgpassword')

    user = binUser
    key = binApi

    configurations = ['archives']
    pkg {
        repo = "android-view-keypad"

        name = "com.housebrandapps:android-view-keypad"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            gpg {
                sign = true
                passphrase = binPwd
            }
        }
    }
}